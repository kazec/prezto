#
# Executes commands at login post-zshrc.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

# Execute code that does not affect the current session in the background.
{
  # Compile the completion dump to increase startup speed.
  zcompdump="${XDG_CACHE_HOME:-$HOME/.cache}/prezto/zcompdump"
  if [[ -s "$zcompdump" && (! -s "${zcompdump}.zwc" || "$zcompdump" -nt "${zcompdump}.zwc") ]]; then
    zcompile "$zcompdump"
  fi
} &!

# Expand alias
_alias_for() {
  local cmd="$(echo $1 | cut -d" " -f1)"
  [[ ($cmd =~ '[[:punct:]]') ]] && return
  local found="$(type $cmd | sed -nE "/^$cmd is an alias for (.+)/s//\\1/p")"
  [[ -n $found ]] && echo "${found%\'}"
}

_expand_command_line() {
  [[ $# -eq 0 ]] && return
  local found_alias="$(_alias_for $1)"
  if [[ -n $found_alias ]]; then
    echo "\033[32mCommand: \033[0;96m$found_alias\033[0m"
  fi
}

autoload -U add-zsh-hook # Load the zsh hook module.
add-zsh-hook preexec _expand_command_line # Adds the hook
